buildscript {
    ext {
        kotlinVersion = '1.1.4-2'
        springBootVersion = '1.5.6.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:$kotlinVersion")
    }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'kotlin-jpa'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

jar {
    baseName = 'choice'
    if (project.hasProperty('buildNumber')) {
        version = "${buildNumber}"
    } else {
        version = 'SNAPSHOT'
    }
    archiveName = "${baseName}.${extension}"
    manifest {
        attributes("Implementation-Title": "CHoice", "Implementation-Version": version)
    }
}

springBoot {
    executable = true
}


sourceCompatibility = 1.8
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/wisvch/maven" }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.springframework.boot:spring-boot-configuration-processor'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlinVersion}"
    compile "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"

    compile 'org.thymeleaf:thymeleaf-spring4:3.0.0.RELEASE'
    compile 'org.thymeleaf:thymeleaf:3.0.0.RELEASE'
    compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity4:3.0.0.RELEASE'

    compile('ch.wisv:wisvch-connect-client:1.2.6.0') {
        // Workaround for https://github.com/mitreid-connect/OpenID-Connect-Java-Spring-Server/pull/1084
        exclude module: 'bcprov-jdk15on'
    }
    compile 'org.bouncycastle:bcprov-jdk15on:1.52+'

    compile 'org.webjars:bootstrap:3.3.7-1'
    compile 'org.webjars.bower:wisvch-bootstrap-theme:0.0.3'
    compile 'org.webjars:font-awesome:4.7.0'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile "io.rest-assured:rest-assured:3.0.2"

    runtime 'org.postgresql:postgresql'
    runtime "org.hsqldb:hsqldb"
}
